#
#
# Library Target: linked against executable and tests.

# Generate a config.vala file, which can be used for version
# information and feature detection.
configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/config.vala.in"
    "${CMAKE_CURRENT_BINARY_DIR}/config.vala"
    @ONLY)

# You can re-type these if you prefer; putting them in variables makes
# it easier to reuse them, and helps explain what each command needs
# to include.

# The name of the C header, without the trailing ".h"
set(MY_PROJECT_HEADER ${PROJECT_NAME})

# These are the Vala sources you want to build your library out of.
set(MY_PROJECT_VALA_SOURCES
    "${CMAKE_CURRENT_BINARY_DIR}/config.vala"
    my_class.vala)

# Any dependencies.  Note that it isn't actually necessary to add
# glib-2.0 or gobject-2.0 (it is done implicitly), but for this
# template project we don't actually depend on anything else, so we
# added it for illustrative purposes.
set(MY_PROJECT_VALA_PACKAGES
    glib-2.0
    gobject-2.0)

# Compile Vala -> C
vala_precompile_target(
    ${MY_PROJECT_LIBRARY_NAME}
    MY_PROJECT_C_SOURCES
    PACKAGES ${MY_PROJECT_VALA_PACKAGES}
    VAPI "${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_PKG_NAME}.vapi"
    GIR "${MY_PROJECT_GIR_NAME}.gir"
    HEADER "${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_PKG_NAME}.h"
    ${MY_PROJECT_VALA_SOURCES})

# Compile the library.
#
# By default, we create and install a shared library.  If you don't
# want to do that, you can change "SHARED" in the below function call
# to "STATIC", and comment out everything *after* this function call.
add_library(${MY_PROJECT_LIBRARY_NAME}
    SHARED
    ${MY_PROJECT_C_SOURCES})

# Generate a .gitignore
file(WRITE  ".gitignore" "# Automatically generated by CMake, do not modify.\n")
foreach(file
    ".gitignore"
    "${CMAKE_SHARED_LIBRARY_PREFIX}${MY_PROJECT_LIBRARY_NAME}${CMAKE_SHARED_LIBRARY_SUFFIX}*"
    "${CMAKE_STATIC_LIBRARY_PREFIX}${MY_PROJECT_LIBRARY_NAME}${CMAKE_STATIC_LIBRARY_SUFFIX}"
    "${CMAKE_SHARED_LIBRARY_PREFIX}${MY_PROJECT_LIBRARY_NAME}${CMAKE_SHARED_LIBRARY_SUFFIX}.${MY_PROJECT_VERSION_API}"
    "${CMAKE_SHARED_LIBRARY_PREFIX}${MY_PROJECT_LIBRARY_NAME}${CMAKE_SHARED_LIBRARY_SUFFIX}.${MY_PROJECT_VERSION_API}.${MY_PROJECT_VERSION_REVISION}"
    "${MY_PROJECT_PKG_NAME}.pc"
    "config.vala")
  file(APPEND ".gitignore" "/${file}\n")
endforeach(file)
foreach(file ${MY_PROJECT_C_SOURCES})
  string(REPLACE "${CMAKE_CURRENT_BINARY_DIR}/" "" file ${file})
  file(APPEND ".gitignore" "/${file}\n")
endforeach(file)

# Define the gettext domain
set_property (TARGET ${MY_PROJECT_LIBRARY_NAME}
    APPEND PROPERTY COMPILE_DEFINITIONS GETTEXT_PACKAGE="${GETTEXT_PACKAGE}")

# If you don't want to build and install a shared library, comment out
# (or remove) everything below this point.

# Set the library's version information
set_target_properties (${MY_PROJECT_LIBRARY_NAME} PROPERTIES
    SOVERSION "${MY_PROJECT_SOVERSION_CURRENT}.${MY_PROJECT_SOVERSION_REVISION}.${MY_PROJECT_SOVERSION_AGE}"
    VERSION   "${MY_PROJECT_SOVERSION_CURRENT}.${MY_PROJECT_SOVERSION_REVISION}")

# GObject Introspection support
find_package(GObjectIntrospection REQUIRED)

# We already had valac write a GIR, now we need to compile it to a
# typelib.
gobject_introspection_compile(
  "${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_GIR_NAME}.typelib"
  "${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_GIR_NAME}.gir")

# Telling CMake you want to install a file doesn't actually get it to
# *build* that file.  The workaround is to add a custom target which
# is always built and depens on the file we want to install.
add_custom_target(typelib ALL DEPENDS "${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_GIR_NAME}.typelib")

# Install our typelib
install(FILES "${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_GIR_NAME}.typelib"
    DESTINATION "${GI_TYPELIB_DIR}")

# Install our GIR
install(FILES "${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_GIR_NAME}.gir"
    DESTINATION "${GI_REPOSITORY_DIR}")

# Install our VAPI
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_PKG_NAME}.vapi
    DESTINATION ${CMAKE_INSTALL_DATADIR}/vala/vapi)

# Install the library
install (TARGETS ${MY_PROJECT_LIBRARY_NAME}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}  # On Windows, the DLL goes in the same place as the executables
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}  # This is where shared libraries (*.so on Linux, *.dynlib on OS X)
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}) # Finally, this is where static libraries (*.a) go

# Install the C header
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_PKG_NAME}.h
    DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR})

# Install our VAPI
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_PKG_NAME}.vapi
    DESTINATION ${CMAKE_INSTALL_DATADIR}/vala/vapi)

# Generate and install a pkg-config file
configure_file (
    "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}.pc.in"
    "${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_PKG_NAME}.pc"
    @ONLY)
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/${MY_PROJECT_PKG_NAME}.pc
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/pkgconfig)

# Generate documentation
find_package(Valadoc)
if(${VALADOC_FOUND})
    valadoc_generate(html
        PACKAGE_NAME ${MY_PROJECT_PKG_NAME}
        PACKAGE_VERSION "${MY_PROJECT_VERSION_MAJOR}.${MY_PROJECT_VERSION_MINOR}.${MY_PROJECT_VERSION_REVISION}"
        SOURCES ${MY_PROJECT_VALA_SOURCES}
        PACKAGES ${MY_PROJECT_VALA_PACKAGES})
    valadoc_generate(devhelp
        DOCLET devhelp
        PACKAGE_NAME ${MY_PROJECT_PKG_NAME}
        PACKAGE_VERSION "${MY_PROJECT_VERSION_MAJOR}.${MY_PROJECT_VERSION_MINOR}.${MY_PROJECT_VERSION_REVISION}"
        SOURCES ${MY_PROJECT_VALA_SOURCES}
        PACKAGES ${MY_PROJECT_VALA_PACKAGES})

    install(FILES "${CMAKE_CURRENT_BINARY_DIR}/devhelp/${MY_PROJECT_PKG_NAME}"
        DESTINATION "${CMAKE_INSTALL_DATADIR}/devhelp/books")
    add_custom_target(doc ALL
        DEPENDS html devhelp)
endif()
